import HoTTModel.TypeStructures
import HoTTModel.Contextual
import HoTTModel.LocallyCartesianClosed.ChosenPullbacks
import HoTTModel.Lemmas.HEq

open ContextualCategory CategoryTheory Limits

namespace Universe
variable {C : Type u} [CategoryTheory.Category.{v, u} C] {U : Universe C}

inductive Chain (U : Universe C) : C ‚Üí C ‚Üí Type (max u v)
| nil X : U.Chain X X
| cons (Y : C) (g : Y ‚ü∂ U.down) (c : U.Chain X Y)  : U.Chain X (U.pt g)

namespace Chain
variable {X Y : C}

def obj (_ : U.Chain X Y) : C := Y

def tailObj : U.Chain X Y ‚Üí C
| nil _ => X
| cons Y _ _ => Y

def tail : (c : U.Chain X Y) ‚Üí U.Chain X c.tailObj
| nil _ => nil X
| cons _ _ c => c

def proj : (c : U.Chain X Y) ‚Üí (Y ‚ü∂ tailObj c)
| nil _ => ùüô X
| cons _ g _ => U.snd g

@[simp]
def ne_nil : U.Chain X Y ‚Üí Prop
| nil _ => False
| cons _ _ _ => True

@[simp]
abbrev is_nil (c : U.Chain X Y) : Prop := ¬¨ c.ne_nil

lemma ne_nil_iff_not_is_nil {c : U.Chain X Y} : c.ne_nil ‚Üî ¬¨ c.is_nil := by
  simp only [not_not]

lemma is_nil_iff_not_ne_nil {c : U.Chain X Y} : c.is_nil ‚Üî ¬¨ c.ne_nil := by rfl

lemma ne_nil.cons {c : U.Chain X Y} : (cons Y g c).ne_nil := by simp [ne_nil]

def tailHom : (c : U.Chain X Y) ‚Üí (h : c.ne_nil) ‚Üí (c.tailObj ‚ü∂ U.down)
| nil _, h => False.elim h
| cons _ g _, _ => g

def length : {Y : C} ‚Üí U.Chain X Y ‚Üí ‚Ñï
| _, nil _ => 0
| _, cons _ _ c => c.length + 1

section LengthLemma

lemma is_nil_iff_length_eq_zero {c : U.Chain X Y} : c.is_nil ‚Üî c.length = 0  := by
  cases c with
  | nil => simp only [is_nil, ne_nil, not_false_eq_true, length]
  | cons _ _ _ => simp [is_nil, length]

lemma ne_nil_iff_length_ne_zero {c : U.Chain X Y} : c.ne_nil ‚Üî c.length ‚â† 0  := by
  simp only [ne_eq, ‚Üê is_nil_iff_length_eq_zero, ne_nil_iff_not_is_nil]

lemma is_nil.of_length_eq_zero {c : U.Chain X Y} (h : length c = 0) : c.is_nil := by
  rwa [is_nil_iff_length_eq_zero]

lemma length_eq_zero_of_is_nil {c : U.Chain X Y} (h : is_nil c) : length c = 0  := by
  rwa [‚Üê is_nil_iff_length_eq_zero]

lemma eq_of_length_eq_zero {c : U.Chain X Y} (h : length c = 0) :
    Y = X := by
  cases c with
  | nil => rfl
  | cons Y g c => simp [length] at h

lemma heq_of_length_eq_zero {c : U.Chain X Y} (h : length c = 0) :
    HEq c (Chain.nil (U:= U) X) := by
  cases c with
  | nil => rfl
  | cons Y g c => simp [length] at h

lemma length_tail {c : U.Chain X Y} :
    length (tail c) = length c - 1 := by
  cases c <;> rfl

lemma length_tail' {c : U.Chain X Y} :
    length c = n + 1 ‚Üí length (tail c) = n := by
  intro h; rw [length_tail, h]; rfl

end LengthLemma

section Lemma

lemma tailHom_cons {g : Y ‚ü∂ U.down} (c : U.Chain X Y) :
  (Chain.cons Y g c).tailHom (by simp) = g := rfl

end Lemma

end Chain

@[ext]
structure Chains (U : Universe C) (X : C) : Type (max u v) where
  dom : C
  chain : U.Chain X dom

@[simp]
def Chain.toChains (c : U.Chain X Y) : U.Chains X where
  dom := Y
  chain := c

namespace Chains

open Chain

section CategoryChains

structure Hom (c d : U.Chains X) where
  hom : c.dom ‚ü∂ d.dom

@[simp]
def id (c : U.Chains X) : Hom c c where
  hom := ùüô c.dom

@[simp]
def comp {c d e : U.Chains X} :
    Hom c d ‚Üí Hom d e ‚Üí Hom c e :=
  fun x y ‚Ü¶ ‚ü®x.hom ‚â´ y.hom‚ü©

instance CategoryChains : Category (U.Chains X) where
  Hom := Hom
  id := id
  comp := comp

@[simp, reassoc]
lemma comp_hom {c d e: U.Chains X} {f : c ‚ü∂ d} {g : d ‚ü∂ e} :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom :=
  rfl

@[simp]
lemma id_hom {c : U.Chains X} :
    (ùüô c : Hom c c).hom = ùüô c.dom :=
  rfl

@[simp]
lemma eqToHom_hom {c d: U.Chains X} (h : c = d):
    (eqToHom h).hom = eqToHom (congrArg dom h):= by
  cases h; simp

@[ext]
lemma hom_ext {c d : U.Chains X} {f g : c ‚ü∂ d} (h : f.hom = g.hom) : f = g := by
  cases f
  cases g
  simp at h
  simp [h]

end CategoryChains

section PreContextualStructure

variable {X : C} {c d : U.Chains X}

@[simp]
def one : U.Chains X := (Chain.nil X).toChains

@[simp]
def gr (c : U.Chains X) : ‚Ñï := c.chain.length

lemma one_uniq {c : U.Chains X} : gr c = 0 ‚Üí c = one := by
  simp
  intro h
  ext
  <;> simp [eq_of_length_eq_zero h, heq_of_length_eq_zero h]

class isTerminal (t : C) where
  is_terminal : IsTerminal t

def is_terminal (t : C) [h : isTerminal t] :
    IsTerminal t :=
  h.is_terminal

instance UniqueToTerminal [h : isTerminal t] (X : U.Chains t) :
    Unique (X ‚ü∂ one) where
  default := ‚ü®(isTerminalEquivUnique _ t h.1 _).default‚ü©
  uniq a := by ext; apply (isTerminalEquivUnique _ t h.1 _).uniq

def one_terminal [isTerminal t] : IsTerminal (@one _ _ U t) :=
  IsTerminal.ofUniqueHom (fun _ ‚Ü¶ default) (fun _ ‚Ü¶ Unique.uniq _)

@[simp]
def ft (c : U.Chains X) : U.Chains X where
  dom := c.chain.tailObj
  chain := c.chain.tail

lemma ft_one : ft (@one _ _ U X) = one :=
  by simp [tailObj, Chain.tail]

lemma ft_gr {n : ‚Ñï} (c : U.Chains X) :
    gr c = n + 1 ‚Üí gr (ft c) = n :=
  length_tail'

def proj (c : U.Chains X) : c ‚ü∂ ft c :=
  ‚ü®c.chain.proj‚ü©

@[simp]
instance instChainsPreContextualCategory [isTerminal t] :
    PreContextualCategory (U.Chains t) where
  gr := gr
  one := one
  one_gr := by aesop
  one_uniq := one_uniq
  one_terminal := one_terminal
  ft := ft
  ft_one := ft_one
  ft_gr := ft_gr
  proj := proj

end PreContextualStructure

section ContextualStructure

open PreContextualCategory
variable {t : C} {d : U.Chains t}
  {Y : C} {p : Y ‚ü∂ U.down} {c : U.Chain t Y}

lemma ne_nil_of_NR  [isTerminal t] [h : NR d] : ne_nil d.chain := by
  rw [ne_nil_iff_not_is_nil, is_nil_iff_length_eq_zero]
  exact h.nr

-- maybe use cases as those below???
def ft_hom [isTerminal t] (c : U.Chains t) [NR c] : (ft c).dom ‚ü∂ U.down :=
  c.chain.tailHom ne_nil_of_NR

def cons (Y : C) (g : Y ‚ü∂ U.down) (e : U.Chain t Y) : U.Chains t :=
  ‚ü®U.pt g, Chain.cons Y g e‚ü©

abbrev cons' (d : U.Chains t) (g : d.dom ‚ü∂ U.down) := cons _ g d.chain

@[simp]
lemma ft_cons' {g : d.dom ‚ü∂ U.down} : (d.cons' g).ft = d := by
  simp [cons', cons, tailObj, tail]

instance [isTerminal t] : NR (cons Y p c) where
  nr := by simp [gr, length]

def pb_cons (f : d ‚ü∂ ft (cons Y p c)) : U.Chains t where
  dom := U.pt (f.hom ‚â´ p)
  chain := Chain.cons d.dom (f.hom ‚â´ p) d.chain

variable {f : d ‚ü∂ ft (cons Y p c)}

-- note `ft (cons Y p c) = c.toChains` definitionally

lemma gr_pb_cons_ne_zero : gr (pb_cons f) ‚â† 0 := by simp [length]

lemma ft_pb_cons: ft (pb_cons f) = d := rfl

lemma pb_fst_cons_comm (f : d ‚ü∂ ft (cons Y p c)) :
    U.fst (f.hom ‚â´ p) ‚â´ U.hom = (U.snd (f.hom ‚â´ p) ‚â´ f.hom) ‚â´ p :=
  by rw [U.comm, Category.assoc]

noncomputable def pb_fst_cons (f : d ‚ü∂ ft (cons Y p c)) : pb_cons f ‚ü∂ (cons Y p c) where
  hom := (U.isPullback p).lift (U.fst (f.hom ‚â´ p)) (U.snd (f.hom ‚â´ p) ‚â´ f.hom)
    (pb_fst_cons_comm _)

lemma pb_fst_cons_comp_fst : (pb_fst_cons f).hom ‚â´ U.fst p = U.fst (f.hom ‚â´ p) :=
  (U.isPullback p).lift_fst _ _ _

lemma pb_fst_cons_comp_snd : (pb_fst_cons f).hom ‚â´ U.snd p = U.snd (f.hom ‚â´ p) ‚â´ f.hom :=
  (U.isPullback p).lift_snd _ _ _

lemma comm_cons :
  (pb_fst_cons f) ‚â´ proj (cons Y p c) =
    ((proj <| pb_cons f) ‚â´ eqToHom (ft_pb_cons (f := f))) ‚â´ f := by
  ext; simp
  apply PullbackCone.IsLimit.lift_snd _ _ _ _

lemma pullback_id_obj_cons : pb_cons (ùüô (ft (cons Y p c))) = cons Y p c := by
  ext
  . simp [pb_cons]; rfl
  . simp [pb_cons, cons, tail, tailObj]; rw [Category.id_comp]

lemma pb_fst_cons_id_map :
    pb_fst_cons (ùüô (ft (cons Y p c))) = eqToHom pullback_id_obj_cons := by
  symm; ext
  simp [pb_fst_cons, cons, tailObj]
  apply (U.isPullback p).hom_ext
  all_goals simp [eqToHom_comp_iff_heq]; congr; simp only [Category.id_comp]

lemma pullback_id_map_cons :
    HEq (pb_fst_cons (ùüô (ft (cons Y p c)))) (ùüô (cons Y p c)) := by
  apply heq_of_eqToHom_comp_eq pullback_id_obj_cons.symm
  rw [pb_fst_cons_id_map]; simp

lemma pullback_comp_obj_cons {g : e ‚ü∂ d} :
    pb_cons (g ‚â´ f) = pb_cons (g ‚â´ eqToHom (ft_pb_cons (f := f)).symm) := by
  simp [pb_cons]
  congr 1; simp only [Category.assoc, Category.comp_id]
/-
lemma pullback_comp_map_cons {g : e ‚ü∂ d} :
    pb_fst_cons (g ‚â´ f) =
      eqToHom (pullback_comp_obj_cons (f := f) (g := g)) ‚â´
        pb_fst_cons (g ‚â´ eqToHom (ft_pb_cons (f := f)).symm) ‚â´ pb_fst_cons f := by
  ext
  apply (U.isPullback p).hom_ext
  . simp
    rw [pb_fst_cons_comp_fst, pb_fst_cons_comp_fst, pb_fst_cons_comp_fst,
        ‚Üê Category.comp_id (eqToHom _ ‚â´ _), ‚Üê eqToHom_refl U.up (Eq.refl U.up),
        Category.assoc, conj_eqToHom_iff_heq _ _ _ (Eq.refl _)]
    congr 1; simp
  . simp
    rw [pb_fst_cons_comp_snd, pb_fst_cons_comp_snd, ‚Üê Category.assoc _ _ f.hom,
        pb_fst_cons_comp_snd, ‚Üê Category.comp_id (_ ‚â´ f.hom),
        ‚Üê eqToHom_refl (dom _) (Eq.refl _), conj_eqToHom_iff_heq _ _ _ (Eq.refl _)]
    simp
    congr 1
    . simp only [pb_cons, Category.assoc, Category.comp_id]
    . congr 1; simp only [Category.assoc, Category.comp_id]
-/

lemma pullback_comp_map_cons {g : e ‚ü∂ d} :
    HEq (pb_fst_cons (g ‚â´ f)) (pb_fst_cons (g ‚â´ eqToHom (ft_pb_cons (f := f)).symm) ‚â´
      pb_fst_cons f) := by
  apply heq_of_eq_eqToHom_comp pullback_comp_obj_cons
  ext
  apply (U.isPullback p).hom_ext
  . simp
    rw [pb_fst_cons_comp_fst, pb_fst_cons_comp_fst, pb_fst_cons_comp_fst,
        ‚Üê Category.comp_id (eqToHom _ ‚â´ _), ‚Üê eqToHom_refl U.up (Eq.refl U.up),
        Category.assoc, conj_eqToHom_iff_heq _ _ _ (Eq.refl _)]
    congr 1; simp
  . simp
    rw [pb_fst_cons_comp_snd, pb_fst_cons_comp_snd, ‚Üê Category.assoc _ _ f.hom,
        pb_fst_cons_comp_snd, ‚Üê Category.comp_id (_ ‚â´ f.hom),
        ‚Üê eqToHom_refl (dom _) (Eq.refl _), conj_eqToHom_iff_heq _ _ _ (Eq.refl _)]
    simp
    congr 1
    . simp only [pb_cons, Category.assoc, Category.comp_id]
    . congr 1; simp only [Category.assoc, Category.comp_id]

def isPullbackFrom {X Y Z W : U.Chains t} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} {h : Y ‚ü∂ W} {i : Z ‚ü∂ W}
  (is : IsPullback f.hom g.hom h.hom i.hom) :
    IsPullback f g h i where
  w := by ext; simp [is.w]
  isLimit' := ‚ü®by
      apply PullbackCone.isLimitAux'; intro s
      use ‚ü®is.lift s.fst.hom s.snd.hom (by rw [‚Üê comp_hom, s.condition, comp_hom])‚ü©
      simp
      constructor; ext; apply PullbackCone.IsLimit.lift_fst
      constructor; ext; apply PullbackCone.IsLimit.lift_snd
      intro m hm hm'
      ext; simp
      apply is.hom_ext
      . simp; rw [‚Üê comp_hom, hm]
      . simp; rw [‚Üê comp_hom, hm']
    ‚ü©

noncomputable def is_pullback_cons:
    IsPullback (pb_fst_cons f) ((pb_cons f).proj ‚â´ eqToHom (ft_pb_cons (f := f)))
      (proj (cons Y p c)) f := isPullbackFrom {
  w := by change (pb_fst_cons f ‚â´ _).hom = ((proj _ ‚â´ _) ‚â´ _).hom; rw [comm_cons]
  isLimit' := ‚ü®by
    apply topSquareIsPullback _ rfl (U.isPullback p).isLimit
    convert (U.isPullback (f.hom ‚â´ p) ).isLimit
    simp [PullbackCone.pasteVert, pb_fst_cons]
    rfl
    ‚ü©}

variable [isTerminal t]

@[elab_as_elim]
def cases_cons {h : (c : U.Chains t) ‚Üí [NR c] ‚Üí Sort w}
  (h' : ‚àÄ {Y p c}, h (cons Y p c)) (c : U.Chains t) [NR c]:
    h c := by
  cases c with
  | mk X c =>
    cases c with
    | nil => rename _ => inst; have := inst.nr; simp [gr, length] at this
    | cons Y g c => exact h'

@[simp]
lemma cases_cons_cons {h : (c : U.Chains t) ‚Üí [NR c] ‚Üí Sort w}
  (h' : ‚àÄ {Y p c}, h (cons Y p c)) {Y : C} {p : Y ‚ü∂ U.down} {c : U.Chain t Y} :
    cases_cons (h := h) h' (cons Y p c) = @h' Y p c :=
  rfl

@[simp]
lemma cases_cons_cons' {h : (c : U.Chains t) ‚Üí [NR c] ‚Üí Sort w}
  (h' : ‚àÄ {Y p c}, h (cons Y p c)) (d : U.Chains t) (g : d.dom ‚ü∂ U.down) :
    cases_cons (h := h) h' (d.cons' g) = @h' d.dom g d.chain :=
  rfl

variable {c d : U.Chains t} [NR c]

def pb (f : d ‚ü∂ ft c) :
    U.Chains t :=
  cases_cons pb_cons c f

noncomputable def pb_fst (f : d ‚ü∂ ft c) :
    pb f ‚ü∂ c :=
  cases_cons (h := fun c ‚Ü¶ (f : d ‚ü∂ ft c) ‚Üí (pb f ‚ü∂ c)) pb_fst_cons c f

lemma gr_pb {f : d ‚ü∂ ft c} :
    gr (pb f) ‚â† 0 :=
  cases_cons (h := fun c ‚Ü¶ (f : d ‚ü∂ ft c) ‚Üí (gr (pb f) ‚â† 0)) (fun _ ‚Ü¶ gr_pb_cons_ne_zero) c f

instance NR_pb {f : d ‚ü∂ ft c} :
    NR (pb f) := ‚ü®gr_pb‚ü©

lemma ft_pb {f : d ‚ü∂ ft c} :
    ft (pb f) = d :=
  cases_cons (h := fun c ‚Ü¶ (f : d ‚ü∂ ft c) ‚Üí (ft (pb f) = d)) (fun _ ‚Ü¶ ft_pb_cons) c f

lemma pullback_id_obj :
    pb (ùüô (ft c)) = c :=
  cases_cons (h := fun c ‚Ü¶ pb (ùüô (ft c)) = c) (fun {Y p c} ‚Ü¶ @pullback_id_obj_cons C _ U t Y p c) c

lemma pullback_id_map :
    HEq (pb_fst (ùüô (ft c))) (ùüô c) :=
  cases_cons (h := fun c ‚Ü¶ HEq (pb_fst (ùüô (ft c))) (ùüô c))
    (fun {Y p c} ‚Ü¶ @pullback_id_map_cons C _ U t Y p c) c

lemma pullback_comp_obj {c d e : U.Chains t} [NR c] {f : d ‚ü∂ ft c} {g : e ‚ü∂ d} :
    pb (g ‚â´ f) = pb (g ‚â´ eqToHom (ft_pb (f := f)).symm) :=
  cases_cons (h := fun c ‚Ü¶ {f : d ‚ü∂ ft c} ‚Üí {g : e ‚ü∂ d} ‚Üí pb (g ‚â´ f) =
    pb (g ‚â´ eqToHom (ft_pb (f := f)).symm))
      (fun {Y p c f} ‚Ü¶ @pullback_comp_obj_cons C _ U t d Y p c f e) c

lemma pullback_comp_map {c d e : U.Chains t} [NR c] {f : d ‚ü∂ ft c} {g : e ‚ü∂ d} :
    HEq (pb_fst (g ‚â´ f)) (pb_fst (g ‚â´ eqToHom (ft_pb (f := f)).symm) ‚â´ pb_fst f) :=
  cases_cons (h := fun c ‚Ü¶ {f : d ‚ü∂ ft c} ‚Üí {g : e ‚ü∂ d} ‚Üí
    HEq (pb_fst (g ‚â´ f)) (pb_fst (g ‚â´ eqToHom (ft_pb (f := f)).symm) ‚â´ pb_fst f))
      (fun {Y p c f} ‚Ü¶ @pullback_comp_map_cons C _ U t d Y p c f e) c

noncomputable def is_pullback (f : d ‚ü∂ ft c) :
    IsPullback (pb_fst f) (proj (pb f) ‚â´ eqToHom ft_pb) (proj c) f :=
  cases_cons
    (h := fun c ‚Ü¶ (f : d ‚ü∂ ft c) ‚Üí (IsPullback (pb_fst f) (proj (pb f) ‚â´ eqToHom ft_pb) (proj c) f))
    (fun _ ‚Ü¶ is_pullback_cons) c f

noncomputable instance instChainsContextualCategory : ContextualCategory (U.Chains t) where
  pb := pb
  pb_fst := pb_fst
  gr_pb := gr_pb
  ft_pb := ft_pb
  isPullback := is_pullback
  pullback_id_obj := pullback_id_obj
  pullback_id_map := pullback_id_map
  pullback_comp_obj := pullback_comp_obj
  pullback_comp_map := pullback_comp_map

end ContextualStructure

section TypeStructures

variable {t : C} [isTerminal t] (Œì : U.Chains t)

abbrev Ext := ContextualCategory.Ext Œì

namespace Ext
section
variable (A : Ext Œì)

-- `Ext` is not nil
lemma obj_chain_ne_nil : A.obj.chain.ne_nil := ne_nil_of_NR

-- `Ext` defines tailHom
@[simp]
def tailHom := A.obj.chain.tailHom A.obj_chain_ne_nil

abbrev proj' : A.obj.dom ‚ü∂ A.obj.ft.dom := (proj A.obj).hom

end

def mk (c : U.Chain t Y) (h : ft ‚ü®_, c‚ü© = Œì) (h' : ne_nil c): Ext Œì where
  obj := ‚ü®_, c‚ü©
  ft' := h
  gr' := by
    change c.length = gr _ + _
    rw [ne_nil_iff_length_ne_zero] at h'
    apply Nat.succ_pred_eq_of_ne_zero at h'
    have := h ‚ñ∏ ft_gr ‚ü®_, c‚ü© h'.symm
    rwa [this, eq_comm]

def rec (F : Ext Œì ‚Üí Sort*)
  (h : ‚àÄ (Y) (c : U.Chain t Y) (h h'), F (Ext.mk Œì c h h')) (c : Ext Œì) :
    F c := by
  cases c with
  | mk c _ gr' =>
    apply h
    change c.chain.length = _ at gr'
    rw [ne_nil_iff_length_ne_zero, gr']
    apply Nat.succ_ne_zero

def rec' (F : Ext Œì ‚Üí Sort*)
  (h : ‚àÄ (g : Œì.dom ‚ü∂ U.down) (c : U.Chain t Œì.dom) h,
    F (Ext.mk Œì (Chain.cons Œì.dom g c) h ne_nil.cons)) (c : Ext Œì) :
    F c := by
  induction c using Ext.rec with
  | h Y c h' h'' =>
      cases c with
    | nil => simp at h''
    | cons Y g c =>
        cases h'; apply h

def mk' (g : Œì.dom ‚ü∂ U.down) : Ext Œì where
  obj := cons' Œì g
  ft' := ft_cons'
  gr' := rfl

def rec'' (F : Ext Œì ‚Üí Sort*)
  (h : ‚àÄ g , F (Ext.mk' Œì g)) (c : Ext Œì) :
    F c := by
  induction c using Ext.rec' with
  | h g c h' =>
    convert h g
    simp [mk, mk', cons, cons']
    simp [ft, tail, tailObj] at h'
    apply_fun Chains.chain at h'
    exact h'

variable (A : Ext Œì)

variable {Œì} in
/--
```
mk Œì ‚Üí U.up
‚Üì       ‚Üì
Œì ‚Üí U.down
```
-/
def mk'.isPullback (g : Œì.dom ‚ü∂ U.down) :
    IsPullback (U.fst g) (mk' Œì g).hom.hom U.hom g := by
  convert U.isPullback g
  simp only [Ext.hom, comp_hom, eqToHom_refl, id_hom, Category.comp_id]
  -- just rewrite this;; it's no good
  rfl

/--
```
A === U.pt ---‚Üí U.up
       |          |
       ‚Üì          ‚Üì
       ft A --‚Üí  U.down
```
-/
lemma obj_eq_pt : A.obj.dom = U.pt A.tailHom := by
  induction A using Ext.rec'' with
  | h g => rfl

def fst : A.obj.dom ‚ü∂ U.up :=
  eqToHom A.obj_eq_pt ‚â´ U.fst A.tailHom

def ft_eq : A.obj.ft.dom = Œì.dom := by simp only [‚Üê A.ft']; rfl

def proj : A.obj.dom ‚ü∂ Œì.dom := A.obj.proj.hom ‚â´ eqToHom A.ft_eq

-- Better name???
def gen : Œì.dom ‚ü∂ U.down := eqToHom (A.ft_eq).symm ‚â´ A.tailHom


variable {Œì}
/--
```
A === U.pt ---‚Üí U.up
|       |          |
‚Üì       ‚Üì          ‚Üì
Œì ===  ft A --‚Üí  U.down
```
-/
lemma isPullback :
    IsPullback A.fst A.hom.hom U.hom A.gen := by
  induction A using Ext.rec'' with
  | h g =>
      simp [mk', fst, Ext.hom, gen]
      convert U.isPullback _

lemma isPullbackLeft' {X : C} (f : Œì.dom ‚ü∂ X) (g : X ‚ü∂ U.down) :
    IsPullback ((mk'.isPullback (f ‚â´ g)).liftIsPullbackOf (U.isPullback g) f rfl)
      (mk' Œì (f ‚â´ g)).hom.hom (U.snd g) f := by
  apply IsPullback.of_right _ _ (U.isPullback g)
  . convert mk'.isPullback (f ‚â´ g)
    simp only [IsPullback.liftIsPullbackOf_fst]
  . simp only [IsPullback.liftIsPullbackOf_snd]

lemma isPullbackLeft {Œì' : U.Chains t} (A : Ext Œì) (f : Œì' ‚ü∂ Œì) :
    IsPullback (A.pullbackFst f).hom (A.pullback f).hom.hom
      A.hom.hom f.hom := by
  induction A using Ext.rec'' with
  | h g =>
    simp [mk', Ext.pullbackFst, Ext.pullback]
    rw [Category.comp_id]
    convert isPullbackLeft' f.hom g using 1
    . simp [ContextualCategory.pb_fst, pb_fst, pb_fst_cons]
      apply (U.isPullback g).hom_ext
      . simp
      . simp [Ext.hom, PreContextualCategory.proj, mk', proj]; rfl
    . simp only [Ext.hom, eqToHom_refl, Category.comp_id]; rfl

lemma pullbackFst_comp_fst {Œì' : U.Chains t} (A : Ext Œì) (f : Œì' ‚ü∂ Œì) :
  (Ext.pullbackFst A f).hom ‚â´ Ext.fst Œì A = Ext.fst Œì' (Ext.pullback A f) := by
  induction A using Ext.rec''
  simp [Ext.pullbackFst, Ext.fst, ContextualCategory.pb_fst, pb_fst,
    Ext.mk', pb_fst_cons]
  exact IsPullback.lift_fst _ _ _ _

end Ext
-- say we want to define the interpretation function from TT for CC.
-- the initial data is `‚ä¢ B`, which should correponds to `Ext 1`
-- so indeed, `A : Ext Œì` is the data to start with

/-
variable {Œì} in
/--
  Equivalence bewteen homs in the over category and sections.
-/
noncomputable def Section.equiv {n} (A : Ext Œì n) :
    (Over.mk (ùüô Œì.dom) ‚ü∂ Over.mk A.hom.hom) ‚âÉ Section A where
  toFun f := by
    apply Over.homMk (Hom.mk f.left) _
    ext; simp only [comp_hom]
    erw [Over.w f]; rfl
  invFun s := by
    apply Over.homMk s.left.hom _
    change (s.left ‚â´ A.hom).hom = _
    erw [Over.w s]; rfl
  left_inv := by intro f; rfl
  right_inv := by intro s; rfl


variable {Œì} in
/--
A section of `Ext` in chain defines a section in the original category.
-/
noncomputable def Section.toHom {n} {A : Ext Œì n} :=
  ‚áë(Section.equiv A).symm

variable {Œì} in
/--
A section of `Ext` in chain from a section in the original category.
-/
noncomputable abbrev Section.ofHom {n} {A : Ext Œì n} :=
  ‚áë(Section.equiv A)
-/

variable {Œì} in
/--
  Equivalence bewteen homs in the over category and sections.
-/
noncomputable def Section.equiv (A : Ext Œì) :
    (Over.mk (ùüô Œì.dom) ‚ü∂ Over.mk A.hom.hom) ‚âÉ Section A where
  toFun f := by
    apply Over.homMk (Hom.mk f.left) _
    ext; simp only [comp_hom]
    erw [Over.w f]; rfl
  invFun s := by
    apply Over.homMk s.left.hom _
    change (s.left ‚â´ A.hom).hom = _
    erw [Over.w s]; rfl
  left_inv := by intro f; rfl
  right_inv := by intro s; rfl


variable {Œì} in
/--
A section of `Ext` in chain defines a section in the original category.
-/
noncomputable abbrev Section.toHom {A : Ext Œì} :=
  ‚áë(Section.equiv A).symm

@[simp]
lemma Section.toHom_left {A : Ext Œì} (f : Section A):
    (Section.toHom f).left = f.left.hom := rfl

variable {Œì} in
/--
A section of `Ext` in chain from a section in the original category.
-/
noncomputable abbrev Section.ofHom {A : Ext Œì} :=
  ‚áë(Section.equiv A)

@[simp]
lemma Section.ofHom_left {A : Ext Œì} (f : Over.mk (ùüô Œì.dom) ‚ü∂ Over.mk A.hom.hom):
    (Section.ofHom f).left.hom = f.left := rfl

noncomputable section

open Pi

namespace Pi
open LocallyCartesianClosed

variable [HasFiniteWidePullbacks C] [LocallyCartesianClosed C] [HasBinaryProducts C]

variable (S : Pi.Structure U) {Œì} (A : Œì.Ext) (B : A.obj.Ext)

def form : Œì.Ext :=
  Ext.mk' Œì (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom)

variable (b : Section B)

def transfer :
    (Œ†A.hom.hom).obj (Over.mk B.hom.hom) ‚âÖ Over.mk (Ext.hom (form S A B)).hom :=
  IsPullback.isoIsPullback_snd_overMk
    ((pushforward.isPullback (IsPullback.form‚ÇÅ.isPullback A.isPullback B.isPullback)
      (IsPullback.form‚ÇÇ.isPullback A.isPullback B.isPullback)).paste_horiz S.isPullback)
      (Ext.mk'.isPullback (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom))

lemma transfer_hom_left_eq_isoIsPullback :
  (transfer S A B).hom.left =
    (IsPullback.isoIsPullback _ _
  ((pushforward.isPullback (IsPullback.form‚ÇÅ.isPullback A.isPullback B.isPullback)
    (IsPullback.form‚ÇÇ.isPullback A.isPullback B.isPullback)).paste_horiz S.isPullback)
    (Ext.mk'.isPullback (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom))).hom := rfl

lemma transfer_hom_left :
  (transfer S A B).hom.left =
    IsPullback.liftIsPullbackAlong
  ((pushforward.isPullback (IsPullback.form‚ÇÅ.isPullback A.isPullback B.isPullback)
    (IsPullback.form‚ÇÇ.isPullback A.isPullback B.isPullback)).paste_horiz S.isPullback)
    (Ext.mk'.isPullback (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom)) (ùüô _) (by simp) := by
  rw [transfer_hom_left_eq_isoIsPullback]
  apply (Ext.mk'.isPullback (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom)).hom_ext
  <;> simp

def intro : Section (form S A B) :=
  Section.ofHom $ IsPullback.adjEquiv (IsPullback.of_id_snd (f := A.hom.hom)) (Over.mk B.hom.hom)
    (Section.toHom b) ‚â´ (transfer S A B).hom

variable (f : Section (form S A B)) (a : Section A)

def reduce : Over.mk (ùüô Œì.dom) ‚ü∂ (Œ†A.hom.hom).obj (Over.mk B.hom.hom) :=
  Section.toHom f ‚â´ (transfer S A B).inv

lemma reduce_intro : reduce S A B (intro S A B b) =
  IsPullback.adjEquiv (IsPullback.of_id_snd (f := A.hom.hom)) (Over.mk B.hom.hom)
    (Section.toHom b) := by
  simp [reduce, intro]

def elim :
    Over.mk a.left ‚ü∂ Over.mk B.hom := by
  refine Over.homMk (Hom.mk ?_) ?_
  exact (Section.toHom a).left ‚â´
    ((IsPullback.of_hasPullback ((Œ†A.hom.hom).obj (Over.mk B.hom.hom)).hom A.hom.hom).sectionSnd'
    (reduce S A B f) ‚â´ (adj A.hom.hom).counit.app (Over.mk B.hom.hom)).left
  ext; simp only [Over.mk_left, Over.comp_left, Over.homMk_left,
    Over.mk_hom, comp_hom, Category.assoc]
  erw [Over.w, Over.w, Over.mk_hom, Category.comp_id]
  rfl

variable {A B} in
lemma compt (a : Section A) (b : Section B) :
    (elim S A B (intro S A B b) a).left = a.left ‚â´ b.left := by
  ext; simp
  change _ = (Section.toHom a).left ‚â´ (Section.toHom b).left
  dsimp only [elim, Over.homMk_left]
  simp_rw [reduce_intro]
  congr
  rw [‚Üê Adjunction.homEquiv_symm_id]
  convert IsPullback.adjEquiv_naturality_symm_left'
    (IsPullback.of_id_snd (f := A.hom.hom))
    ((IsPullback.adjEquiv _ (Over.mk (Ext.hom B).hom)) (Section.toHom b)) (ùüô _)
  simp only [Functor.id, Category.comp_id, Equiv.symm_apply_apply]

variable {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) (A : Œì.Ext) (B : A.obj.Ext)

@[reassoc]
lemma form_stable_form‚ÇÄ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) (A : Œì.Ext) (B : A.obj.Ext) :
    f.hom ‚â´ IsPullback.form‚ÇÄ A.isPullback B.isPullback =
      IsPullback.form‚ÇÄ (Ext.isPullback (A.pullback f))
        (Ext.isPullback (B.pullback (A.pullbackFst f))) := by
  revert A; apply Ext.rec''
  intro g B
  -- lemma???
  have aux : (f.hom ‚â´ IsPullback.form‚ÇÄ (Ext.mk' Œì g).isPullback B.isPullback) ‚â´ Pi.hom U =
      Ext.gen Œì' (Ext.pullback (Ext.mk' Œì g) f) := by
    simp [Ext.gen, Ext.pullback, Ext.mk', cons', cons,
      ContextualCategory.pb, pb, cases_cons, pb_cons, Chain.tailHom]
  apply IsPullback.form‚ÇÄ_ext‚ÇÇ _ aux _
  revert B; apply Ext.rec''
  intro g' aux
  conv_rhs =>
    simp [Ext.gen, Ext.pullback, Ext.mk', cons', cons, ContextualCategory.pb,
      pb, cases_cons, pb_cons, Chain.tailHom]
  change _ ‚â´ Gen‚ÇÇ.hom U = ((Ext.mk' Œì g).pullbackFst _).hom ‚â´ g'
  set q : Over.mk (Ext.gen Œì' (Ext.pullback (Ext.mk' Œì g) f)) ‚ü∂ op U :=
    Over.homMk (f.hom ‚â´ IsPullback.form‚ÇÄ (Ext.mk' Œì g).isPullback _) aux
  have : Ext.gen (Ext.mk' Œì g).obj (Ext.mk' (Ext.mk' Œì g).obj g') = g' := by
    simp [Ext.gen, Ext.pullback, Ext.mk', cons', cons, Chain.tailHom]
  rw [‚Üê this, ‚Üê Pi.IsPullback.form‚ÇÅ_comp_Gen‚ÇÇhom (Ext.mk' Œì g).isPullback
    (Ext.mk' _ g').isPullback, ‚Üê Category.assoc _ _ (Gen‚ÇÇ.hom U)]
  congr 1
  apply (U.isPullback (Pi.hom U)).hom_ext
  . simp; rw [Pi.IsPullback.form‚ÇÅ_comp_fst]
    simp [Ext.gen, Ext.mk', Ext.pullback, Ext.fst, Ext.pullbackFst, ContextualCategory.pb,
      ContextualCategory.pb_fst, Chains.cons', Chains.cons, pb, pb_fst,
      cases_cons, pb_cons, pb_fst_cons, Chain.tailHom]
  . simp [q]; rw [(Pi.IsPullback.form‚ÇÅ.isPullback _ _).w, ‚Üê comp_hom_assoc,  ‚Üê comp_hom_assoc,
      ((Ext.mk' Œì g).pullbackIsPullback f).w]

lemma form_stable_form‚ÇÅ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) (A : Œì.Ext) (B : A.obj.Ext) :
    (Ext.pullbackFst A f).hom ‚â´ IsPullback.form‚ÇÅ A.isPullback B.isPullback =
      IsPullback.form‚ÇÅ (Ext.isPullback (A.pullback f))
        (Ext.isPullback (B.pullback (A.pullbackFst f))) := by
  apply (U.isPullback _).hom_ext
  . rw [Category.assoc, IsPullback.form‚ÇÅ_comp_fst,
      IsPullback.form‚ÇÅ_comp_fst, Ext.pullbackFst_comp_fst]
  . rw [Category.assoc, (IsPullback.form‚ÇÅ.isPullback _ _ ).w,
      (IsPullback.form‚ÇÅ.isPullback _ _ ).w, ‚Üê comp_hom_assoc,
      (Ext.pullbackIsPullback _ _).w, comp_hom_assoc, form_stable_form‚ÇÄ]

lemma form_stable_form‚ÇÇ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) (A : Œì.Ext) (B : A.obj.Ext) :
    (Ext.pullbackFst B (Ext.pullbackFst A f)).hom ‚â´ IsPullback.form‚ÇÇ A.isPullback B.isPullback =
      IsPullback.form‚ÇÇ (Ext.isPullback (A.pullback f))
        (Ext.isPullback (B.pullback (A.pullbackFst f))) := by
  apply (U.isPullback _).hom_ext
  . rw [Category.assoc, IsPullback.form‚ÇÇ_comp_fst,
      IsPullback.form‚ÇÇ_comp_fst, Ext.pullbackFst_comp_fst]
  . rw [Category.assoc, (IsPullback.form‚ÇÇ.isPullback _ _ ).w,
      (IsPullback.form‚ÇÇ.isPullback _ _ ).w, ‚Üê comp_hom_assoc,
      (Ext.pullbackIsPullback _ _).w, comp_hom_assoc, form_stable_form‚ÇÅ]

lemma form_stable {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) (A : Œì.Ext) (B : A.obj.Ext) :
    (form S A B).pullback f = form S (A.pullback f) (B.pullback (A.pullbackFst f)) := by
  ext : 1
  simp only [form, Ext.pullback, Ext.mk']
  simp [‚Üê form_stable_form‚ÇÄ]
  rfl

lemma over_heq_of_hom_heq {C : Type u‚ÇÅ} [CategoryTheory.Category.{v‚ÇÅ, u‚ÇÅ} C]
  {X X' : C}  {f g : Over X} {f' g' : Over X'} {œÜ : f ‚ü∂ g} {œà : f' ‚ü∂ g'}
  (h‚ÇÅ : X = X') (h‚ÇÇ : HEq f f') (h‚ÇÉ : HEq g g')
  (h : HEq œÜ.left œà.left):
    HEq œÜ œà := by
  cases h‚ÇÅ; cases h‚ÇÇ; cases h‚ÇÉ
  simp; ext; simpa using h

omit [HasBinaryProducts C] in
lemma intro_stable_aux‚ÇÅ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) {A : Ext Œì}
  (B : Ext A.obj) (b : Section B) :
    IsPullback.adjEquiv (IsPullback.of_id_snd (f := (Ext.pullback A f).hom.hom)) _
      (Section.toHom (Section.lift (Ext.pullbackFst A f) b)) =
      (pushforward.isPullback (Ext.isPullbackLeft A f)
        (Ext.isPullbackLeft B (Ext.pullbackFst A f))).sectionSnd'
        (IsPullback.adjEquiv (IsPullback.of_id_snd (f := A.hom.hom)) _ (Section.toHom b)) := by
  convert (pushforward.adj_lift_eq_lift_adj
    (Ext.isPullbackLeft A f) (Ext.isPullbackLeft B (Ext.pullbackFst A f))
    (IsPullback.of_id_snd (f := (Ext.pullback A f).hom.hom))
    (IsPullback.of_id_snd (f := A.hom.hom)) (IsPullback.of_id_snd (f := f.hom)).toCommSq
    (Section.toHom b)).symm
  ext; simp [Section.lift]
  apply (Ext.isPullbackLeft B (Ext.pullbackFst A f)).hom_ext
  . simp; rw [‚Üê comp_hom, IsPullback.liftIsPullbackAlong_fst, comp_hom]
    congr 1
    conv_lhs => rw [‚Üê Category.id_comp (Ext.pullbackFst A f).hom]
    conv_rhs => rw [‚Üê Category.comp_id (pushforward.liftAux _ _ _ _)]
    erw [IsPullback.liftIsPullbackAlong_snd]; rfl
  . simp; rw [‚Üê comp_hom, IsPullback.liftIsPullbackAlong_snd]; rfl

lemma intro_stable_eq {S} {Œì Œì' : U.Chains t} {f : Œì' ‚ü∂ Œì} {A : Ext Œì} {B : Ext A.obj} :
    (Over.mk (Ext.hom (form S (Ext.pullback A f)
      (Ext.pullback B (Ext.pullbackFst A f))))).left =
      (Over.mk (Ext.pullback (form S A B) f).hom).left := by
  symm; congr <;> apply form_stable

lemma temp‚ÇÅ :
    eqToHom (congrArg dom intro_stable_eq) ‚â´ Ext.fst Œì' (Ext.pullback (form S A B) f)
      = U.fst (IsPullback.form‚ÇÄ (Ext.isPullback (A.pullback f))
      (Ext.isPullback (B.pullback (A.pullbackFst f))) ‚â´ S.hom) := by
  simp [Ext.fst, Ext.pullback, ContextualCategory.pb, pb, form, Ext.mk', pb_cons, Chain.tailHom]
  rw [eqToHom_comp_iff_heq, Category.comp_id, form_stable_form‚ÇÄ_assoc]

lemma temp‚ÇÇ :
    Ext.fst Œì' (Ext.pullback (form S A B) f) =
      (Ext.pullbackFst (form S A B) f).hom ‚â´ Ext.fst Œì (form S A B) := by
  simp [Ext.fst, Ext.pullback, Ext.pullbackFst, ContextualCategory.pb_fst, pb_fst,
    form, Ext.mk', pb_fst_cons, ContextualCategory.pb, pb, pb_cons]
  simp only [tailHom, ne_nil, cons, IsPullback.lift_fst]

lemma temp‚ÇÉ :
    eqToHom (congrArg dom intro_stable_eq) ‚â´ (Ext.pullback (form S A B) f).hom.hom
      = (Ext.mk' Œì' (IsPullback.form‚ÇÄ (Ext.isPullback (A.pullback f))
      (Ext.isPullback (B.pullback (A.pullbackFst f))) ‚â´ S.hom)).hom.hom := by
  simp [Ext.hom, Ext.pullback, PreContextualCategory.proj, proj, ContextualCategory.pb, pb,
    form, Ext.mk', pb_cons, cons', Chain.proj, Chains.cons, cases_cons]
  rw [eqToHom_comp_iff_heq, Category.comp_id, form_stable_form‚ÇÄ_assoc]

lemma intro_stable_aux‚ÇÄ :
  (transfer S (Ext.pullback A f) (Ext.pullback B (Ext.pullbackFst A f))).hom.left ‚â´
    eqToHom (congrArg dom intro_stable_eq) =
    (pushforward.isPullback (Ext.isPullbackLeft A f)
      (Ext.isPullbackLeft B (Ext.pullbackFst A f))).liftIsPullbackAlong
      (Ext.isPullbackLeft (form S A B) f)
      (transfer S A B).hom.left (Over.w (transfer S A B).hom) := by
  apply (Ext.isPullback _).hom_ext
  . conv_rhs => rw [temp‚ÇÇ]
    simp [transfer_hom_left]; rw [IsPullback.liftIsPullbackAlong_fst_assoc]
    rw [temp‚ÇÅ, IsPullback.liftIsPullbackAlong_fst]
    have : U.fst (IsPullback.form‚ÇÄ A.isPullback B.isPullback ‚â´ S.hom) =
      Ext.fst Œì (form S A B) := by
        simp [Ext.fst]; rfl
    rw [‚Üê this, IsPullback.liftIsPullbackAlong_fst]
    simp
    rw [pushforward.trans_comp'_assoc]
    congr 2
    rw [form_stable_form‚ÇÄ]
    rw [form_stable_form‚ÇÅ]
    rw [form_stable_form‚ÇÇ]
  . simp [transfer_hom_left, temp‚ÇÉ]

lemma intro_stable_aux‚ÇÇ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) {A : Ext Œì} (B : Ext A.obj) :
    (transfer S (Ext.pullback A f) (Ext.pullback B (Ext.pullbackFst A f))).hom.left ‚â´
      eqToHom (congrArg dom intro_stable_eq) ‚â´ (Ext.pullbackFst (form S A B) f).hom =
      pushforward.trans (Ext.isPullbackLeft A f)
        (Ext.isPullbackLeft B (Ext.pullbackFst A f)).toCommSq ‚â´ (transfer S A B).hom.left := by
  rw [‚Üê Category.assoc]
  convert (pushforward.isPullback (Ext.isPullbackLeft A f)
    (Ext.isPullbackLeft B (Ext.pullbackFst A f))).liftIsPullbackAlong_fst
      (Ext.isPullbackLeft (form S A B) f) (transfer S A B).hom.left (Over.w _) using 2
  apply intro_stable_aux‚ÇÄ

lemma intro_stable_aux‚ÇÉ {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) {A : Ext Œì} (B : Ext A.obj) :
    (transfer S (Ext.pullback A f) (Ext.pullback B (Ext.pullbackFst A f))).hom.left ‚â´
      eqToHom (congrArg dom intro_stable_eq) ‚â´ (Ext.pullback (form S A B) f).hom.hom =
      ((Œ†(Ext.pullback A f).hom.hom).obj (Over.mk
        (Ext.pullback B (Ext.pullbackFst A f)).hom.hom)).hom := by
  rw [‚Üê Category.assoc]
  convert (pushforward.isPullback (Ext.isPullbackLeft A f)
    (Ext.isPullbackLeft B (Ext.pullbackFst A f))).liftIsPullbackAlong_snd
      (Ext.isPullbackLeft (form S A B) f) (transfer S A B).hom.left (Over.w _) using 2
  apply intro_stable_aux‚ÇÄ

lemma intro_stable {Œì Œì' : U.Chains t} (f : Œì' ‚ü∂ Œì) {A : Ext Œì} (B : Ext A.obj) (b : Section B) :
    HEq ((intro S A B b).lift f)
      (intro S (A.pullback f) (B.pullback (A.pullbackFst f)) (b.lift (A.pullbackFst f))) := by
  apply over_heq_of_hom_heq (by rfl) (by rfl)
  . simp; congr
    <;> exact form_stable S f A B
  . rw [heq_comm, ‚Üê comp_eqToHom_iff_heq intro_stable_eq]
    ext; conv_lhs => simp only [intro, intro_stable_aux‚ÇÅ]
    apply (Ext.isPullbackLeft (form S A B) f).hom_ext
    . conv_rhs => erw [‚Üê comp_hom, IsPullback.sectionSnd'_left_fst]
      simp [intro, intro_stable_aux‚ÇÇ]
      erw [IsPullback.sectionSnd'_left_fst_assoc]
    . conv_rhs => erw [‚Üê comp_hom, Over.w _]
      simp [intro_stable_aux‚ÇÉ]

end Pi

variable [HasFiniteWidePullbacks C] [LocallyCartesianClosed C] [HasBinaryProducts C] in
open Pi in
def Pi_type (S : Pi.Structure U) : Pi_type (U.Chains t) where
  form := form S
  intro := intro S
  elim := elim S
  compt := compt S
  form_stable := form_stable S
  intro_stable := intro_stable S
  elim_stable := sorry

/-
section
-- maybe it would be good to rewrite every isterminal to hasterminal
-- and use the classical terminal throughout
variable [HasFiniteWidePullbacks C] [LocallyCartesianClosed C] [HasTerminal C] (S : Empty.Structure U)

namespace Empty

variable (Œì : U.Chains (‚ä§_ C))

instance : isTerminal (‚ä§_ C) where
  is_terminal := terminalIsTerminal

def form : Ext Œì :=
  Ext.mk' Œì (terminal.from Œì.dom ‚â´ S.map)

open LocallyCartesianClosed

def form.obj_dom_isInitial' : IsInitial (Over.mk (form S Œì).hom.hom) :=
  IsPullbackPreservesInitial (Ext.isPullbackLeft Œì (terminal.from Œì.dom) S.map) S.is_initial

def form.obj_dom_isInitial : IsInitial (Over.mk (form S Œì).hom) := by
  apply IsInitial.ofUniqueHom _ _
  . intro Y
    apply Over.homMk (Hom.mk ((form.obj_dom_isInitial' S Œì).to (Over.mk Y.hom.hom)).left) _
    ext; simp; erw [Over.w]; rfl
  . intro Y m
    ext; simp
    set m' : Over.mk (form S Œì).hom.hom ‚ü∂ Over.mk Y.hom.hom := by
      apply Over.homMk m.left.hom (by simp; erw [‚Üê comp_hom, Over.w]; rfl)
    change m'.left = _
    congr
    apply (form.obj_dom_isInitial' S Œì).hom_ext

variable {Œì}

def elim‚ÇÄ (A : Ext (form S Œì).obj) :
    (Over.mk (ùüô (form S Œì).obj)).left ‚ü∂ (Over.mk (Ext.hom A)).left :=
   ((form.obj_dom_isInitial S Œì).to (Over.mk (A.hom ‚â´ (form S Œì).hom))).left

def elim (A : Ext (form S Œì).obj) : Section A := by
  apply Over.homMk (elim‚ÇÄ S A) _
  dsimp
  set e : Over.mk (form S Œì).hom ‚ü∂ Over.mk (form S Œì).hom := by
    refine Over.homMk (elim‚ÇÄ S A ‚â´ A.hom) ?_
    simp; apply Over.w
  change e.left = (ùüô Over.mk (form S Œì).hom).left
  congr
  apply (form.obj_dom_isInitial S Œì).hom_ext

omit [HasFiniteWidePullbacks C] [LocallyCartesianClosed C] in
lemma form_stable {Œì Œì' : U.Chains (‚ä§_ C)} (f : Œì' ‚ü∂ Œì) :
    form S Œì' = Ext.pullback (form S Œì) f := by
  ext
  all_goals
  simp only [Ext.pullback, form, Ext.mk', Chains.cons', cons, ContextualCategory.pb, pb, cases_cons,
      pb_cons]
  rw [‚Üê Category.assoc]
  congr 2
  apply terminal.hom_ext

variable {Œì Œì' : U.Chains (‚ä§_ C)} (f : Œì' ‚ü∂ Œì)

lemma elim_stable {Œì Œì' : U.Chains (‚ä§_ C)} (A : Ext (form S Œì).obj) (f : Œì' ‚ü∂ Œì) :
  elim S (Ext.pullback A (eqToHom (congrArg Ext.obj (form_stable S f)) ‚â´ ((form S Œì).pullbackFst f)))
      = (elim S A).lift (eqToHom (congrArg Ext.obj (form_stable S f)) ‚â´ (form S Œì).pullbackFst f) := by
  ext : 1
  set e := elim S _
  apply Over.IsInitial_hom_ext (form.obj_dom_isInitial S Œì')
  . simp; rw [‚Üê Category.assoc]
    change (e.left ‚â´ _) ‚â´ _ = _
    erw [Over.w]
    simp only [Over.mk_left, Over.mk_hom, Category.id_comp]
  . simp; erw [‚Üê Category.assoc, Over.w]
    simp only [Over.mk_hom, Category.id_comp]

end Empty

open Empty in
def Empty_type (S : Empty.Structure U) : Empty_type (U.Chains (‚ä§_ C)) where
  form := form S
  elim := elim S
  form_stable := form_stable S
  elim_stable := elim_stable S

end

section

def aux {C : Type*} [Category C] {P X Y Z : C}
  {fst : P ‚ü∂ X} {snd : P ‚ü∂ Z} {f : X ‚ü∂ Y} {g : Z ‚ü∂ Y} [IsIso f] (is : IsPullback fst snd f g) :
    P ‚âÖ Z where
  hom := snd
  inv := is.sectionSnd (inv f) (IsIso.inv_hom_id _)
  hom_inv_id := by
    apply is.hom_ext
    . simp; rw [‚Üê Category.assoc, ‚Üê is.w]; simp
    . simp
  inv_hom_id := by simp

variable [HasTerminal C] (S : Unit.Structure U)

namespace Unit

variable (Œì : U.Chains (‚ä§_ C))

def form : Ext Œì :=
  Ext.mk' Œì (terminal.from Œì.dom ‚â´ S.map)

instance : IsIso (U.snd S.map) where
  out := by
    use S.iso.inv.left
    constructor
    . have := Over.w S.iso.hom
      simp at this
      simp only [‚Üê this, ‚Üê Over.comp_left, S.iso.hom_inv_id, Over.id_left, Over.mk_left]
    . exact Over.w S.iso.inv

def intro' : Over.mk (ùüô _) ‚âÖ Over.mk (form S Œì).hom where
  hom := Section.ofHom $ Over.homMk (aux (Ext.isPullbackLeft Œì (terminal.from Œì.dom) S.map)).inv
    (Iso.inv_hom_id _)
  inv := Over.homMk (form S Œì).hom
  hom_inv_id := by
    ext
    simp [Section.ofHom, Section.equiv]
    exact (aux _).inv_hom_id
  inv_hom_id := by
    ext
    simp [Section.ofHom, Section.equiv]
    exact (aux _).hom_inv_id

def intro : Section (form S Œì) := (intro' S Œì).hom

def intro'Left : (form S Œì).obj ‚âÖ Œì where
  hom := (form S Œì).hom
  inv := (intro' S Œì).hom.left
  hom_inv_id := by
    change (Over.homMk (form S Œì).hom : Over.mk (Ext.hom (form S Œì)) ‚ü∂ Over.mk (ùüô Œì)).left ‚â´ _ = _
    erw [‚Üê Over.comp_left, (intro' S Œì).inv_hom_id]
    rfl
  inv_hom_id := by
    change _ ‚â´ (Over.homMk (form S Œì).hom : Over.mk (Ext.hom (form S Œì)) ‚ü∂ Over.mk (ùüô Œì)).left = _
    erw [‚Üê Over.comp_left, (intro' S Œì).hom_inv_id]
    rfl

instance : IsIso (intro S Œì) := by dsimp [intro]; infer_instance

instance : IsIso (form S Œì).hom := by change IsIso (intro'Left _ _).hom; infer_instance

variable {Œì} (A : Ext (form S Œì).obj) (d : Over.mk (intro S Œì).left ‚ü∂ Over.mk A.hom)

def elim_tm : Section A := by
  refine Over.homMk ((intro' S Œì).inv.left ‚â´ d.left) ?_
  simp; erw [Over.w d]; simp only [Over.mk_left, Over.mk_hom, ‚Üê Over.comp_left]
  erw [Iso.inv_hom_id]
  rfl

lemma elim_comm : (intro S Œì).left ‚â´ (elim_tm S A d).left = d.left := by
  ext
  simp only [Over.mk_left, elim_tm, Over.homMk_left, comp_hom, ‚Üê Category.assoc]
  change (intro S Œì ‚â´ _).left.hom ‚â´ _ =  _
  erw [(intro' S Œì).hom_inv_id]
  simp only [Over.mk_left, Over.id_left, id_hom, Category.id_comp]

lemma form_stable {Œì'} (f : Œì' ‚ü∂ Œì) : form S Œì' = (form S Œì).pullback f := by
  ext
  all_goals
  simp only [Ext.pullback, form, Ext.mk', Chains.cons', cons, ContextualCategory.pb, pb, cases_cons,
        pb_cons]
  rw [‚Üê Category.assoc]
  congr 2
  apply terminal.hom_ext

lemma aux' {A B : Ext Œì} (eq : A = B) : A.hom = eqToHom (congrArg Ext.obj eq) ‚â´ B.hom := by
  cases eq
  simp only [eqToHom_refl, Category.id_comp]

example {A B : Ext Œì} (eq : A.obj = B.obj) : A.hom = eqToHom eq ‚â´ B.hom := by
  cases B
  cases eq
  simp only [eqToHom_refl, Category.id_comp]

lemma intro_stable {Œì'} (f : Œì' ‚ü∂ Œì) :
    intro S Œì' ‚â´ eqToHom (congrArg (fun f ‚Ü¶ Over.mk f.hom) (form_stable S f))
      = (intro S Œì).lift f := by
  change (intro' S Œì').hom ‚â´ _ = _
  rw [‚Üê Iso.eq_inv_comp]
  dsimp [intro']
  ext : 1
  simp
  have : (form S Œì').hom  =
    eqToHom (congrArg Ext.obj $ form_stable S f) ‚â´ ((form S Œì).pullback f).hom := by
      apply aux' (form_stable S f)
  simp only [this, Category.assoc, aux, Category.comp_id]
  dsimp only [Section.lift, IsPullback.sectionSnd', Over.homMk_left]
  conv => left; rw [‚Üê Category.comp_id (eqToHom _)]
  congr
  convert ((aux $ (form S Œì).pullbackIsPullback f).hom_inv_id).symm
  simp [aux]
  congr
  apply (IsIso.inv_eq_of_hom_inv_id (intro'Left _ _).hom_inv_id).symm



end Unit
end
-/
